[Subsistema nuevo]
		INITIAL	X$EMPLEMOSTRADOR,1
		INITIAL	X$EMPLEPREPARADOR,2
		INITIAL	X$EMPLEFACTURADOR,1
		INITIAL	X$EMPLEHABLADOR,1
		INITIAL	X$NLINEAS,1
ENPROCESO	STORAGE	50000
TOMOOTRO	TABLE		M1,400,50,30
SINTERQUI	TABLE		S$ENPROCESO,1,1,500
SINTERPED	TABLE		S$ENPROCESO,1,1,500
SINTERDES	TABLE		S$ENPROCESO,1,1,500
TIEMPO	TABLE		M1,500,60,30
CANTPROD	FUNCTION	RN2,C4
0,1/0.6,13/0.95,21/1,101
CANTPIEZAS	FUNCTION	RN3,C7
0,1/0.14,21/0.31,51/0.53,201/0.82,501/0.99,2001/1,15001
EMPLEADOSPEDIDOS	VARIABLE	X$EMPLEMOSTRADOR+X$EMPLEPREPARADOR
EMPLEADOHABLADOR	VARIABLE	10+X$EMPLEHABLADOR
EMPLEPREPARADOR		VARIABLE	1+X$EMPLEMOSTRADOR
EMPLEFACTURADOR		VARIABLE	30+X$EMPLEFACTURADOR
LINEAS	VARIABLE	20+X$NLINEAS
;SUBSISTEMA RELOJ
	GENERATE	,,,1
	ADVANCE	36000
	TERMINATE	1
;SUBSISTEMA TABULADOR
	GENERATE	900
	TABULATE	SINTERQUI
	TERMINATE
;SUBSISTEMA ARRIBO CLIENTES
	GENERATE	420,300,1
	ENTER		ENPROCESO
	ASSIGN	NPRODUCTOS,FN$CANTPROD
	ASSIGN	NPIEZAS,FN$CANTPIEZAS
	SELECT MIN	4,1,V$EMPLEADOSPEDIDOS,,Q
	QUEUE		*4
	SEIZE		*4
	DEPART	*4
	ADVANCE	120,60
	ASSIGN	AUX,P$NPRODUCTOS
TOMO	ADVANCE	10,2
	LOOP		AUX,TOMO
	TABULATE	SINTERPED
	;RELEASE	*4
	TRANSFER	,PREPARARL
;SUBSISTEMA ARRIBO LLAMADAS
		GENERATE	600,360,10
		SELECT NU	6,21,V$LINEAS,,,NOLINEA
		SEIZE		*6
		TRANSFER	0.5,OTRAPER
		ENTER		ENPROCESO
		ASSIGN	NPRODUCTOS,FN$CANTPROD
		ASSIGN	NPIEZAS,FN$CANTPIEZAS
		SELECT MIN	4,1,V$EMPLEADOSPEDIDOS,,Q
		SEIZE		*4
		ADVANCE	180,60
		ASSIGN	AUX,P$NPRODUCTOS
TOMOPRODL	ADVANCE	7,2
		LOOP		AUX,TOMOPRODL
		TABULATE	SINTERPED
		TRANSFER	,PREPARARL
;SUBSISTEMA EMPLEADO HABLADOR
OTRAPER	SELECT NU	5,11,V$EMPLEADOHABLADOR,,,NOHABLADOR
		SEIZE		*5
		ADVANCE	120,60
		RELEASE	*5
		RELEASE	*6
		TERMINATE
;SUBSISTEMA EMPLEADO PREPARADOR
PREPARAR	SELECT MIN	4,V$EMPLEPREPARADOR,V$EMPLEADOSPEDIDOS,,Q
		QUEUE		*4
		SEIZE		*4
		DEPART	*4
PREPARARL	ADVANCE 300,120
		ASSIGN	AUX,P$NPRODUCTOS
PREPPROD	ADVANCE	5,2
		LOOP		AUX,PREPPROD
		ASSIGN	AUX,P$NPIEZAS
PREPPIEZ	ADVANCE	3,1
		TEST	GE	P$AUX,20,LISTO
		ASSIGN	AUX-,20
		TRANSFER	,PREPPIEZ
LISTO		RELEASE	*4
		TABULATE	TOMOOTRO
		TRANSFER	,PAGAR
;SUBSISTEMA EMPLEADO FACTURADOR
PAGAR		SELECT MIN	4,31,V$EMPLEFACTURADOR,,Q
		QUEUE		*4
		SEIZE		*4
		DEPART	*4
		ADVANCE	120,30
		ASSIGN	AUX,P$NPRODUCTOS
PAGOPROD	ADVANCE	5,2
		LOOP		AUX,PAGOPROD
		TABULATE	SINTERDES
		RELEASE	*4
		TEST NE	*6,0,SEGUIR
		RELEASE	*6
SEGUIR	LEAVE		ENPROCESO
		TABULATE	TIEMPO
		TERMINATE
NOLINEA	SAVEVALUE	NOATIENDEN+,1
		TERMINATE
NOHABLADOR	SAVEVALUE	NOHABLAN+,1
		RELEASE	*6
		TERMINATE
		


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
